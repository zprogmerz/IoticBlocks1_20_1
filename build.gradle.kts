plugins {
    id("java-library")
    id("maven-publish")
    id("net.neoforged.moddev").version("2.0.141")
    id("idea")
}

object ModConfig {
    const val MINECRAFT_VERSION = "1.21.1"
    const val MINECRAFT_VERSION_RANGE = "[1.21.1]"
    const val NEO_VERSION = "21.1.233"
    const val NEO_VERSION_RANGE = "[21.1,)"
    const val PARCHMENT_MAPPING_VERSION = "2024.11.17"
    const val LOADER_VERSION_RANGE = "[1,)"

    const val MOD_ID = "examplemod"
    const val MOD_NAME = "Example Mod"
    const val MOD_LICENSE = "MIT"
    const val MOD_VERSION = "2.6.0"
    const val MOD_GROUP_ID = "io.github.mochi_753.examplemod"
    const val MOD_DISPLAY_URL = "https://github.com/mochi-753/MDK-1.21.1-ModDevGradle"
    const val MOD_AUTHORS = "Mochi753"
    const val MOD_DESCRIPTION = "EXAMPLE MOD"

    const val CURSEFORGE_PROJECT_ID = "1576170"
    const val MODRINTH_PROJECT_ID = "MOXi22S6"
}

tasks.named<Wrapper>("wrapper").configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = ModConfig.MOD_VERSION
group = ModConfig.MOD_GROUP_ID

sourceSets.main.get().resources {
    srcDir("src/generated/resources")
    exclude("src/generated/**/.cache")
}

repositories {
    mavenCentral()

    /*
    exclusiveContent {
        forRepository {
            maven {
                name = "CurseForge"
                url = uri("https://cursemaven.com")
            }
        }
        filter {
            includeGroup("curse.maven")
        }
    }
     */

    /*
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = uri("https://api.modrinth.com/maven")
            }
        }
        filter {
            includeGroup("maven.modrinth")
        }
    }
     */
}

base {
    @Suppress("MISSING_DEPENDENCY_SUPERCLASS_IN_TYPE_ARGUMENT")
    archivesName.set(ModConfig.MOD_NAME.replace(' ', '-'))
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
        vendor.set(JvmVendorSpec.ADOPTIUM)
    }
}

neoForge {
    version = ModConfig.NEO_VERSION

    parchment {
        mappingsVersion.set(ModConfig.PARCHMENT_MAPPING_VERSION)
        minecraftVersion.set(ModConfig.MINECRAFT_VERSION)
    }

    runs {
        create("client") {
            client()
            systemProperty("neoforge.enabledGameTestNamespaces", ModConfig.MOD_ID)
            gameDirectory.set(file("runs/client"))
        }

        create("client2") {
            client()
            programArguments.addAll("--username", "Dev2")
            systemProperty("neoforge.enabledGameTestNamespaces", ModConfig.MOD_ID)
            gameDirectory.set(file("runs/client2"))
        }

        create("server") {
            server()
            programArgument("--nogui")
            systemProperty("neoforge.enabledGameTestNamespaces", ModConfig.MOD_ID)
            gameDirectory.set(file("runs/server"))
        }

        create("gameTestServer") {
            type = "gameTestServer"
            gameDirectory.set(file("runs/server"))
            systemProperty("neoforge.enabledGameTestNamespaces", ModConfig.MOD_ID)
        }

        create("data") {
            data()
            programArguments.addAll(
                "--mod",
                ModConfig.MOD_ID,
                "--all",
                "--output",
                file("src/generated/resources").absolutePath,
                "--existing",
                file("src/main/resources").absolutePath
            )
            gameDirectory.set(file("runs/data"))
        }

        configureEach {
            systemProperty("forge.logging.markers", "REGISTRIES")
            logLevel.set(org.slf4j.event.Level.DEBUG)
        }
    }

    mods {
        create(ModConfig.MOD_ID) {
            sourceSet(sourceSets.main.get())
        }
    }
}

val localRuntime = configurations.create("localRuntime")
configurations {
    runtimeClasspath {
        extendsFrom(localRuntime)
    }
}

dependencies {
}

val generateModMetadata = tasks.register<ProcessResources>("generateModMetadata") {
    val replaceProperties = mapOf(
        "minecraft_version" to ModConfig.MINECRAFT_VERSION,
        "minecraft_version_range" to ModConfig.MINECRAFT_VERSION_RANGE,
        "neo_version" to ModConfig.NEO_VERSION,
        "neo_version_range" to ModConfig.NEO_VERSION_RANGE,
        "loader_version_range" to ModConfig.LOADER_VERSION_RANGE,
        "mod_id" to ModConfig.MOD_ID,
        "mod_name" to ModConfig.MOD_NAME,
        "mod_license" to ModConfig.MOD_LICENSE,
        "mod_version" to ModConfig.MOD_VERSION,
        "mod_display_url" to ModConfig.MOD_DISPLAY_URL,
        "mod_authors" to ModConfig.MOD_AUTHORS,
        "mod_description" to ModConfig.MOD_DESCRIPTION
    )
    inputs.properties(replaceProperties)

    expand(replaceProperties)
    from("src/main/templates")
    into("build/generated/sources/modMetadata")
}

sourceSets.main {
    resources.srcDir(generateModMetadata)
}

neoForge.ideSyncTask(generateModMetadata)

tasks.withType<JavaCompile>().configureEach {
    options.encoding = "UTF-8"
}

idea {
    module {
        isDownloadJavadoc = true
        isDownloadSources = true
    }
}

tasks.withType<JavaExec>().configureEach {
    standardInput = System.`in`
}

val exportModMetadata = tasks.register("exportModMetadata") {
    val output = layout.buildDirectory.file("libs/metadata.json")
    outputs.file(output)

    doLast {
        val file = output.get().asFile
        file.parentFile.mkdirs()

        val metadata = """
            {
                "modID": "${ModConfig.MOD_ID}",
                "modName": "${ModConfig.MOD_NAME}",
                "modVersion": "${ModConfig.MOD_VERSION}",
                "curseForgeProjectID": "${ModConfig.CURSEFORGE_PROJECT_ID}",
                "modrinthProjectID": "${ModConfig.MODRINTH_PROJECT_ID}"
            }
        """.trimIndent()

        file.writeText(metadata)
    }
}

tasks.named("assemble") {
    dependsOn(exportModMetadata)
}
